///////////////////////////////////////////////////////////////////////////////
/// 
/// Authors: Joshua Claeys, modifications by Doug Zwick
/// Copyright 2017, DigiPen Institute of Technology
///
///////////////////////////////////////////////////////////////////////////////

//-------------------------------------------------------------- Ui Scroll Area
class UiScrollArea : ZilchComponent
{
  [Dependency] var UiWidget : UiWidget;
  
  // How many pixels to scroll for each tick of the mouse wheel.
  [Property] var ScrollSpeed = 35.0;
  [Property][Slider(0, 0.5, 0.05)] var ScrollAnimateTime = 0.2;
  
  [Property] var OverScrollTop = 0.0;
  [Property] var OverScrollBottom = 0.0;
  [Property] var OverScrollLeft = 0.0;
  [Property] var OverScrollRight = 0.0;
  
  [Property][Group("Internals")] var ContentCog = CogPath("Content");
  [Property][Group("Internals")] var ContentHolderCog = CogPath("ContentHolder");
  [Property][Group("Internals")] var ScrollBarXCog = CogPath("ScrollBarX");
  [Property][Group("Internals")] var ScrollBarYCog = CogPath("ScrollBarY");
  
  var Content : UiWidget;
  var ContentHolder : UiWidget;
  var ScrollBarX : UiWidget;
  var ScrollBarY : UiWidget;
  
  var DesiredOffset : Real2;
  
  // 
  var TranslateSequence : ActionSequence;
  
  //***************************************************************************
  var ContentOffset : Real2
  {
    get { return this.ContentOffsetInternal; }
    set { this.ContentOffsetInternal = value; this.Snap(); this.DesiredOffset = this.ContentOffsetInternal; }
  }
  
  //***************************************************************************
  var ContentOffsetInternal : Real2
  {
    get { return this.Content.LocalTopLeft * Real2(-1, 1); }
    set { this.Content.LocalTopLeft = value * Real2(-1, 1); }
  }
  
  //***************************************************************************
  // Content size with the over-scroll taken into account
  var ScrollableContentSize : Real2
  {
    get
    {
      var scrollableClientSize = this.Content.Size;
      scrollableClientSize.X += this.OverScrollLeft + this.OverScrollRight;
      scrollableClientSize.Y += this.OverScrollTop + this.OverScrollBottom;
      return scrollableClientSize;
    }
  }
  
  //***************************************************************************
  var OverScrollTopLeft : Real2
  {
     get { return Real2(this.OverScrollLeft, this.OverScrollTop); }
   }
  
  //***************************************************************************
  function Initialize(init : CogInitializer)
  {
    this.Content = this.ContentCog.UiWidget;
    this.ContentHolder = this.ContentHolderCog.UiWidget;
    this.ScrollBarX = this.ScrollBarXCog.UiWidget;
    this.ScrollBarY = this.ScrollBarYCog.UiWidget;
    this.ScrollBarXCog.UiScrollBar.UiScrollArea = this;
    this.ScrollBarYCog.UiScrollBar.UiScrollArea = this;
    
    Zero.Connect(this.Owner, Events.MouseScroll, this.OnMouseScroll);
    Zero.Connect(this.Owner, Events.UiPreUpdate, this.OnPreTransformUpdate);
    Zero.Connect(this.Owner, Events.UiPostUpdate, this.OnPostTransformUpdate);
    
    this.ContentOffset = Real2(0);
  }
  
  //***************************************************************************
  function AttachToContent(widget : UiWidget)
  {
    widget.Owner.AttachTo(this.Content.Owner);
  }
  
  //***************************************************************************
  function ScrollToTopLeft()
  {
    this.ContentOffset = Real2(0);
  }
  
  //***************************************************************************
  // The given widget is expected to be a child of the 'Content' Cog.
  function ScrollToWidget(widget : UiWidget, animateTime : Real)
  {
    var worldRect = widget.WorldRectangle;
    this.ScrollToWorldRectangle(worldRect, animateTime);
  }
  
  //***************************************************************************
  function ScrollToWorldRectangle(worldRect : Rectangle, animateTime : Real)
  {
    var worldToContentLocal = Math.Invert(this.Content.Owner.Transform.WorldMatrix);
    var localRect = worldRect.Transformed(worldToContentLocal);
    
    this.ScrollToLocalRectangle(localRect, animateTime);
  }
  
  //***************************************************************************
  // Expects the rectangle to be in the local space of the 'Content' Cog.
  function ScrollToLocalRectangle(localRect : Rectangle, animateTime : Real)
  {
    // The area currently visible in the scroll area
    var visibleContentRect = this.UiWidget.WorldRectangle;
    
    var worldToContentLocal = Math.Invert(this.Content.Owner.Transform.WorldMatrix);
    visibleContentRect.Transform(worldToContentLocal);
    
    // Snap top / bottom
    if(localRect.Bottom < visibleContentRect.Bottom)
      visibleContentRect.Bottom = localRect.Bottom;
    else if (localRect.Top > visibleContentRect.Top)
      visibleContentRect.Top = localRect.Top;
    
    // Snap left / right
    if(localRect.Right > visibleContentRect.Right)
      visibleContentRect.Right = localRect.Right;
    else if (localRect.Left < visibleContentRect.Left)
      visibleContentRect.Left = localRect.Left;
    
    // If we have an animation from scrolling, we need to cancel it
    if(this.TranslateSequence != null)
      this.TranslateSequence.Cancel();
    
    var desiredOffset = visibleContentRect.TopLeft * Real2(1, -1);
    if(animateTime <= 0.016)
    {
      this.ContentOffset = desiredOffset;
    }
    else
    {
      this.TranslateSequence = Action.Sequence(this.Owner.Actions);
      Action.Property(this.TranslateSequence, @this.ContentOffset, desiredOffset, animateTime, Ease.QuadOut);
    }
  }
  
  //***************************************************************************
  function OnPreTransformUpdate(event : UiTransformUpdateEvent)
  {
    // We're controlling content manually, so it shouldn't be in layout
    this.Content.InLayout = false;
    
    var contentHolderSize = this.ContentHolder.Size;
    var contentSize = this.Content.Size;
    
    if(this.Content.SizePolicyX == UiSizePolicy.Flex)
      contentSize.X = contentHolderSize.X - this.Content.MarginRight;
    if(this.Content.SizePolicyY == UiSizePolicy.Flex)
      contentSize.Y = contentHolderSize.Y - this.Content.MarginBottom;
    
    this.Content.Size = contentSize;
  }
  
  //***************************************************************************
  function OnPostTransformUpdate(event : UiTransformUpdateEvent)
  {
    var ourSize = this.UiWidget.Size;
    
    var scrollableContentSize = this.ScrollableContentSize;
    
    // What percentage of the entire scrollable client area is visible at one time
    var percentageVisible = ourSize / (scrollableContentSize + Real2(0.00001));
    
    // How far we've scrolled for each axis
    var percentScrolled = (this.ContentOffset + this.OverScrollTopLeft) / (scrollableContentSize - ourSize + Real2(0.00001));
    
    for(var axis = 0; axis < 2; ++axis)
    {
      var bar = this.ScrollBarX;
      if(axis == 1)
        bar = this.ScrollBarY;
      
      // If we have enough room to display the entire client area, disable the
      // scroll bar and move on
      if(ourSize[axis] >= scrollableContentSize[axis])
      {
        bar.Active = false;
        continue;
      }
      
      bar.Active = true;
      
      // Size the handle
      var handle = bar.Owner.UiScrollBar.Handle;
      var handleSize = handle.Size;
      handleSize[axis] = (bar.Size[axis] * percentageVisible[axis]);
      handle.Size = handleSize;
      
      // Move the handle
      var topLeft = handle.LocalTopLeft;
      topLeft[axis] = (bar.Size[axis] - handleSize[axis]) * percentScrolled[axis];
      if(axis == 1)
        topLeft[axis] *= -1;
      handle.LocalTopLeft = topLeft;
    }
  }
  
  //***************************************************************************
  function Scroll(scroll : Real2)
  {
    if (this.ScrollAnimateTime <= 0.016)
    {
      this.ContentOffset -= scroll;
    }
    else
    {
      var newOffset = this.DesiredOffset - scroll;
      var oldOffset = this.ContentOffset;
      
      this.ContentOffsetInternal = newOffset;
      this.Snap();
      newOffset = this.ContentOffset;
      
      if(newOffset != this.DesiredOffset)
      {
        this.DesiredOffset = newOffset;
        this.ContentOffsetInternal = oldOffset;
      
        if(this.TranslateSequence != null)
          this.TranslateSequence.Cancel();
        
        this.TranslateSequence = Action.Sequence(this.Owner.Actions);
        Action.Property(this.TranslateSequence, @this.ContentOffsetInternal, newOffset, this.ScrollAnimateTime, Ease.Linear);
      }
    }
  }
  
  //***************************************************************************
  function OnMouseScroll(event : ViewportMouseEvent)
  {
    if(event.HandledEvent)
      return;
    event.HandledEvent = true;
    var scroll = event.Scroll * this.ScrollSpeed;
    scroll *= Real2(-1, 1);
    
    if(event.ShiftPressed)
    {
      scroll.X = scroll.Y;
      scroll.Y = 0;
    }
    
    this.Scroll(scroll);
  }
  
  //***************************************************************************
  function Snap()
  {
    var ourSize = this.UiWidget.Size;
    this.ContentOffsetInternal = Math.Max(this.ContentOffset, -this.OverScrollTopLeft);
    this.ContentOffsetInternal = Math.Min(this.ContentOffset, Math.Max(this.ScrollableContentSize - this.OverScrollTopLeft - ourSize, Real2(0)));
  }
  
  //***************************************************************************
  function SetScrollPercent(axis : Integer, percent : Real)
  {
    var offset = this.ContentOffset;
    offset[axis] = percent * (this.ScrollableContentSize[axis] - this.UiWidget.Size[axis]);
    this.ContentOffset = offset;
  }
}

//--------------------------------------------------------------- Ui Scroll Bar
class UiScrollBar : ZilchComponent
{
  [Dependency] var UiWidget : UiWidget;
  [Dependency] var MouseCapture : MouseCapture;
  
  [Property] var Axis = 0;
  
  var Handle : UiWidget;
  var UiScrollArea : UiScrollArea;
  
  //var DragStartPosition : Real;
  var GrabOffset : Real;
  
  //***************************************************************************
  function Initialize(init : CogInitializer)
  {
    this.Handle = this.Owner.FindChildByName("Handle").UiWidget;
    
    Zero.Connect(this.Handle.Owner, Events.LeftMouseDown, this.OnLeftMouseDown);
    Zero.Connect(this.Owner, Events.MouseDragMove, this.OnMouseDragMove);
  }
  
  //***************************************************************************
  function OnLeftMouseDown(event : ViewportMouseEvent)
  {
    //this.DragStartPosition = this.Handle.LocalTranslation[this.Axis];
    
    var mousePosWorld = event.ToWorldZPlane(0).XY;
    var mousePosLocal = this.Handle.TransformPointInverse(mousePosWorld);
    this.GrabOffset = mousePosLocal[this.Axis];
    
    this.MouseCapture.Capture(event);
  }
  
  //***************************************************************************
  function OnMouseDragMove(event : ViewportMouseEvent)
  {
    var mousePosWorld = event.ToWorldZPlane(0).XY;
    var mousePosLocal = this.UiWidget.TransformPointInverse(mousePosWorld);
    
    // New top left for the handle
    var newHandlePosition = this.Handle.LocalTopLeft;
    newHandlePosition[this.Axis] = mousePosLocal[this.Axis] - this.GrabOffset;
    
    var barSize = this.UiWidget.Size[this.Axis];
    var handleSize = this.Handle.Size[this.Axis];
    
    //
    var percentageScrolled = newHandlePosition[this.Axis] / (barSize - handleSize);
    if(this.Axis == 1)
      percentageScrolled *= -1.0;
      
    Console.WriteLine(mousePosLocal.X, percentageScrolled);
    
    this.UiScrollArea.SetScrollPercent(this.Axis, percentageScrolled);
  }
}